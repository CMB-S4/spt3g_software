set(Spt3g_VERSION @SPT3G_VERSION@)

@PACKAGE_INIT@

foreach(lib @SPT3G_LIBRARIES@)
	set(Spt3g_LIBRARIES ${Spt3g_LIBRARIES} spt3g::${lib})
endforeach(lib @SPT3G_LIBRARIES@)

if(NOT "@PACKAGE_CMAKE_INSTALL_DIR@" STREQUAL "") # installed
	set_and_check(Spt3g_INCLUDE_DIR "@PACKAGE_INCLUDE_INSTALL_DIR@")
	set_and_check(_core_dir "@PACKAGE_INCLUDE_INSTALL_DIR@/core")
	set(Spt3g_INCLUDE_DIRS ${Spt3g_INCLUDE_DIR} ${_core_dir})

	include("@PACKAGE_CMAKE_INSTALL_DIR@/Spt3gIncludes.cmake")
	include("@PACKAGE_CMAKE_INSTALL_DIR@/Spt3gTargets.cmake")

	set(CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH} "@PACKAGE_CMAKE_INSTALL_DIR@")
	check_required_components(Spt3g)
else(NOT "@PACKAGE_CMAKE_INSTALL_DIR@" STREQUAL "") # not installed
	set(Spt3g_INCLUDE_DIRS "@CMAKE_SOURCE_DIR@/core/include/core")
	foreach(lib @SPT3G_LIBRARIES@)
		list(APPEND Spt3g_INCLUDE_DIRS "@CMAKE_SOURCE_DIR@/${lib}/include")
	endforeach(lib @SPT3G_LIBRARIES@)
	set(Spt3g_INCLUDE_DIR "@CMAKE_SOURCE_DIR@/core/include")

	include("@CMAKE_BINARY_DIR@/cmake/Spt3gIncludes.cmake")
	include("@CMAKE_BINARY_DIR@/cmake/Spt3gTargets.cmake")

	set(CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH} "@CMAKE_SOURCE_DIR@/cmake")
	set(Spt3g_FOUND true)
endif(NOT "@PACKAGE_CMAKE_INSTALL_DIR@" STREQUAL "")

include(CMakeFindDependencyMacro)

set(THREADS_PREFER_PTHREAD_FLAG @THREADS_PREFER_PTHREAD_FLAG@)
find_dependency(Threads REQUIRED)

find_dependency(Python COMPONENTS Interpreter Development REQUIRED)

set(Boost_USE_STATIC_LIBS @Boost_USE_STATIC_LIBS@)
set(Boost_USE_MULTITHREADED @Boost_USE_MULTITHREADED@)
set(Boost_USE_STATIC_RUNTIME @Boost_USE_STATIC_RUNTIME@)
set(Boost_PYTHON_VERSION @Boost_PYTHON_VERSION@)
find_dependency(Boost COMPONENTS system iostreams filesystem python REQUIRED)

if(@FLAC_FOUND@)
	find_dependency(FLAC MODULE)
endif()
if(@NetCDF_FOUND@)
	find_dependency(NetCDF MODULE)
endif()
if(@OpenMP_FOUND@)
	find_dependency(OpenMP)
endif()

include(FindPackageHandleStandardArgs)
find_package_handle_standard_args(Spt3g
	REQUIRED_VARS Spt3g_INCLUDE_DIR Spt3g_INCLUDE_DIRS Spt3g_LIBRARIES
	VERSION_VAR Spt3g_VERSION)
